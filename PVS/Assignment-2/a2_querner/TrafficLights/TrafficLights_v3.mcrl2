sort
	CardinalDirection = struct north | east | south | west; % 4 directions

sort
	Colour = struct red | green | yellow; % 3 colors which each traffic light can have

map
	next: Colour -> Colour;
	safe: Colour # Colour # Colour # Colour -> Bool;

var
	n: Colour;
	e: Colour;
	s: Colour;
	w: Colour;

eqn
	next(red) = green;
	next(green) = yellow;
	next(yellow) = red;

	% it is safe if trafficlights on different axis are not yellow or green at the same time
	safe(n,e,s,w) = !((!(n in {red}) || !(s in {red})) && (!(w in {red}) || !(e in {red}))); 


%------------------------------------------
% definition of a trafficlight
%------------------------------------------
act
  show : CardinalDirection # Colour; % show the color which the given trafficlight currently has

proc
	% shows the current color and change to next color
	TrafficLight(d: CardinalDirection, c: Colour) = show(d, next(c)). TrafficLight(d, next(c));

	% set start configuration. set all to red.
	TrafficLight(d:CardinalDirection) = colourVisible(d, red) . TrafficLight(d, red);




%-------------------------------------------
% Definition of a system consisting of four trafficlights
%-------------------------------------------

act
	receive: CardinalDirection # Colour;
	intersectionUnsafe: Colour # Colour # Colour # Colour;
	colourVisible: CardinalDirection # Colour;

proc
	Monitor(n: Colour, e: Colour, s: Colour, w: Colour) =
		(!safe(n,e,s,w)) 
			-> intersectionUnsafe(n,e,s,w) % if state is unsafe show it
			<> sum c: Colour, d: CardinalDirection .
				(d == north && e == red && w == red) -> receive(north, c) . Monitor(n = c) <>
				(d == east && n == red && s == red) -> receive(east, c) . Monitor(e = c) <>
				(d == south && e == red && w == red) -> receive(south, c) . Monitor(s = c) <>
				(d == west && n == red && s == red) -> receive(west, c) . Monitor(w = c);
	
	Crossing = 
		allow({colourVisible, intersectionUnsafe}, comm({show | receive -> colourVisible},
			TrafficLight(north) 
			|| TrafficLight(east) 
			|| TrafficLight(south) 
			|| TrafficLight(west)
			|| Monitor(red, red, red, red)));


init
	Crossing;
